#version 450

out vec4 FragColour;

in vec2 uvs;

in mat3 TBNmatrix;
in vec3 debugColour;

// projection
uniform mat4 modelMatrix;

// lighting
uniform vec3 fromLight1;
uniform vec3 fromLight2;
uniform float light1Multiplier;
uniform float light2Multiplier;
uniform vec3 lightColour1;
uniform vec3 lightColour2;

// textures
uniform sampler2D diffuseMap;
uniform sampler2D normalMap;
uniform sampler2D specularMap;

// specular calculations
uniform vec3 cameraForward;
uniform float specularPower;

void main()
{
	// calculate the world space normals using the tangeant space and object space normals	
	vec3 tangeantSpaceNormals = texture(normalMap, uvs).xyz * 2.0 - 1.0;
	vec3 objectSpaceNormals = TBNmatrix * tangeantSpaceNormals;
	vec3 worldSpaceNormals = normalize((modelMatrix * vec4(objectSpaceNormals, 0.0)).xyz);

	vec3 specularHighlights1 = texture(specularMap, uvs).xyz * pow(dot(reflect(fromLight1, worldSpaceNormals), cameraForward), specularPower);
	vec3 specularHighlights2 = texture(specularMap, uvs).xyz * pow(dot(reflect(fromLight2, worldSpaceNormals), cameraForward), specularPower);

	vec3 lightVal = max(dot(worldSpaceNormals, -fromLight1), 0.0) * lightColour1 *light1Multiplier;
	lightVal += max(dot(worldSpaceNormals, -fromLight2), 0.0) * lightColour2 * light2Multiplier;

	FragColour = texture(diffuseMap, uvs) * vec4(lightVal, 1) + vec4(specularHighlights1, 1.0f) + vec4(specularHighlights2, 1.0f);

	// debug fragment colours
	//FragColour = vec4(specularHighlights, 1);
}